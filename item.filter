// =============================================================================================
//
// Synpoo's ultra strict PoD Filter
// v0.1.6
// Last updated: March 17 2021
// Reach me at discord: synpoo#7377
//
// This filter was built for the endgame ONLY and WILL hide majority of items
// Do not use if you are a new player
//
// =============================================================================================



// =============================================================
// BASIC ITEMS
//
// Hidden: All potions except supers and full rejuv, keys, scrolls
// =============================================================
ItemDisplay[GOLD<6000]:
ItemDisplay[leg]: %NAME%
ItemDisplay[hp1 OR hp2 OR hp3 OR hp4 OR mp1 OR mp2 OR mp3 OR mp4 OR rvs]:
ItemDisplay[gps OR ops OR gpm OR opm OR gpl OR opl]:
ItemDisplay[key OR vps OR yps OR wms OR tsc OR isc]:
ItemDisplay[hp5]: Heal
ItemDisplay[mp5]:	Mana
ItemDisplay[rvl]: Full

// =============================================================
// GEMS
//
// Hidden: All except flawless skull and all Perfects
// =============================================================
ItemDisplay[skl OR skz]: %NL% %TAN%%NAME% %NL%
ItemDisplay[GEMLEVEL=1 OR GEMLEVEL=2 OR GEMLEVEL=3 OR GEMLEVEL=4]:

// =============================================================
// ENDGAME QUEST ITEMS AND POD ITEMS
//
// =============================================================
// Essences
ItemDisplay[tes]: %GRAY%  Essence%ORANGE% (Andariel and Duriel)  %NOTIFY-ORANGE%
ItemDisplay[ceh]: %GRAY%  Essence%ORANGE% (Mephisto)  %NOTIFY-ORANGE%
ItemDisplay[bet]: %GRAY%  Essence%ORANGE% (Diablo)  %NOTIFY-ORANGE%
ItemDisplay[fed]: %GRAY%  Essence%ORANGE% (Baal)  %NOTIFY-ORANGE%

// Keys and Organs
ItemDisplay[pk1]: %GRAY%  Key%ORANGE% (Countess)  %NOTIFY-ORANGE%
ItemDisplay[pk2]: %GRAY%  Key%ORANGE% (Summoner)  %NOTIFY-ORANGE%
ItemDisplay[pk3]: %GRAY%  Key%ORANGE% (Izual)  %NOTIFY-ORANGE%
ItemDisplay[dhn OR bey OR mbr]: %ORANGE%  %NAME%  %NOTIFY-ORANGE%

// PoD Items
ItemDisplay[cx5]: %NL% %PURPLE%+ %NAME% + %NL%%NOTIFY-PURPLE%
ItemDisplay[cx7]: %NAME%%NOTIFY-ORANGE%
ItemDisplay[maz]: %RED%%NAME%%NOTIFY-RED%
ItemDisplay[(ma1 OR ma2 OR ma3 OR ma4 OR ma5 OR ma6 OR ma7 OR ma8 OR ma9 OR ma10) !ID]: %NL% + %NAME% + %NOTIFY-ORANGE%%NL%

// =============================================================
// RUNES, JEWELS, CHARMS
//
// Hidden: Runes below Lem (Ko shows for unique armor upgrade recipe)
// =============================================================
ItemDisplay[RUNE>25]: %NL% ++      %NAME%      ++ %NL%%NOTIFY-ORANGE%
ItemDisplay[RUNE>19 AND RUNE<26]: %NL% +  %NAME%  + %NL%%NOTIFY-ORANGE%
ItemDisplay[RUNE=18]: %NL% + %NAME% + %NL%%NOTIFY-ORANGE%
ItemDisplay[RUNE>0 AND RUNE<20]:

// Hidden: Unid Large charms, Grand charms <ilvl50, Small charms <ilvl50
ItemDisplay[cm1 OR cm2 OR cm3 (!ID UNI)]: %NL%  %NAME%  %NL%%NOTIFY-GOLD%
ItemDisplay[cm1 OR cm3 (!ID AND ILVL>50)]: %NL%  %NAME% [i%ILVL%]  %NL%%NOTIFY-BLUE%
ItemDisplay[(cm1 OR cm2 OR cm3) !ID]:

// Hidden: Unid Jewels <ilvl 45
ItemDisplay[jew MAG !ID]: %NAME% [i%ILVL%]
ItemDisplay[jew RARE !ID]: %NL%  %NAME% [i%ILVL%]  %NL%%NOTIFY-YELLOW%
ItemDisplay[jew UNI !ID]: %NL%+    %NAME%    +%NL%%NOTIFY-GOLD%
ItemDisplay[jew MAG OR RARE (!ID AND ILVL<45)]:

// =============================================================
// AMULETS, RINGS, BOLTS, ARROWS
//
// Hidden: (UNID) Magic amulets amulets/rings/quivers/arrows
// - Unid Set rings
// - Unid rare amulets/rings/arrows <ilvl74
// =============================================================
ItemDisplay[amu SET !ID]: %NL% +     %NAME%     + %NL%%NOTIFY-GREEN%
ItemDisplay[amu OR rin (UNI !ID)]: %NL% +    %NAME%    + %NL%%NOTIFY-GOLD%
ItemDisplay[amu OR rin (!ID RARE AND ILVL>74)]: + %NAME% [i%ILVL%] +%NOTIFY-YELLOW%
ItemDisplay[aqv OR aq2 (!ID RARE AND ILVL>74)]: %NAME% [i%ILVL%]
ItemDisplay[(amu OR rin OR aq2 OR cq2 OR aqv OR cqv) (NMAG OR MAG OR RARE) !ID]:
ItemDisplay[(aq2 OR cq2 OR aqv OR cqv) UNI !ID]:
ItemDisplay[rin SET !ID]:

// =============================================================
// WEAPONS
//
// =============================================================

//Vendor items
//SK88=1 =32453 Gold. SK73=1 =32453 Gold.
ItemDisplay[WP12 NMAG !ETH (wnd OR ywn OR 9wn) ILVL>49 (SK73>0 OR SK79=3 OR SK82=3 OR SK83>1 OR SK85=3 OR SK86>1 OR SK87>0 OR SK88>0 OR SK89>0 OR SK90>1 OR SK91>1 OR SK92>0 OR SK93>0 OR SK94>0 OR SK95>0)]: %NAME% %ORANGE%($%PRICE%)
ItemDisplay[WP12 NMAG !ETH SOCK=1 (SK73>0 OR SK79=3 OR SK82=3 OR SK83>1 OR SK85=3 OR SK86>1 OR SK87>0 OR SK88>0 OR SK89>0 OR SK90>1 OR SK91>1 OR SK92>0 OR SK93>0 OR SK94>0 OR SK95>0)]: %NAME% %ORANGE%($%PRICE%)
ItemDisplay[NMAG !ETH !ED=15 (7tk OR 7ta OR 7bk)]: Throw%ORANGE% ($%PRICE%)

// UNIQUE
// titans, tstroke, windforce
ItemDisplay[UNI (ama OR amf OR 6lw) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// death cleaver, executioners justice, bartucs
ItemDisplay[UNI (7wa OR 7gi OR 9tw) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// gull, fleshripper, death's web
ItemDisplay[UNI (dgr OR 7kr OR 7gw) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// occy, eschutas, fathom
ItemDisplay[UNI (oba OR obc OR obf) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// reaper's toll, grandfather
ItemDisplay[UNI (7s8 OR 7gd) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// ondals, mang song, azurewrath, lightsabre
ItemDisplay[UNI (6cs OR 6ws OR 7cr) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%

// SET
// nats wep, tals wep
ItemDisplay[SET (7qr OR oba) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GREEN%

// RUNEWORD BASES
// 4os GMB+3 skill
ItemDisplay[amc NMAG SOCK=4 TABSK0=3 !RW]: %NL%  %DARK_GREEN%%NAME% %RED%+3bow %SOCKETS%os  %NL%%NOTIFY-GRAY%
// 4os+ eth thresher/giant thresher/cryptic axe/mancatcher
ItemDisplay[(7s8 OR 7wc OR 7pa OR 7br) NMAG SOCK>3 ETH !RW]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os ETH  %NL%%NOTIFY-GRAY%
// 5os+  superior zerker
ItemDisplay[7wa SUP SOCK>4 !RW]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os  %NL%%NOTIFY-GRAY%
// 5os+ eth superior zerker, col blade, col sword
ItemDisplay[(7wa OR 7gd OR 7fb) SUP SOCK>4 ETH !RW]: %NL%  %DARK_GREEN%%NAME% %RED%%SOCKETS%os ETH  %NL%%NOTIFY-GRAY%
// 3os elite sin claw +3 light sentry
ItemDisplay[!NORM CL5 NMAG SOCK=3 SK271=3 !RW]: %NL%  %DARK_GREEN%%NAME% %RED%+3 Sentry %SOCKETS%os  %NL%%NOTIFY-GRAY%

// hide all else
ItemDisplay[WEAPON (MAG OR RARE OR UNI OR SET) !ID]:
ItemDisplay[WEAPON NMAG !RW]:

// =============================================================
// ARMOR
//
// =============================================================

// UNIQUE
// arreats, jalal's, ravenlore, griffs
ItemDisplay[UNI (baa OR dra OR dre OR ci3) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// tgods, arach, dungo
ItemDisplay[UNI (zhb OR ulc OR umc) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// vmagi, shaft, skullders, que-hegan
ItemDisplay[UNI (xea OR xhn OR xpl OR xtp) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// arkaines, gbane, tyrael
ItemDisplay[UNI (upl OR utu OR uar) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// silks, war trav, sdancer
ItemDisplay[UNI (xmb OR xtb OR uhb) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// chance, dracs, steelrends
ItemDisplay[UNI (mgl OR uvg OR uhg) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// stealskull, valk wing, vampgaze
ItemDisplay[UNI (xlm OR xhm OR xh9) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// arreats, jalal, ravenlore
ItemDisplay[UNI (baa OR dra OR dre) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// shako, steelshade, nightwings veil
ItemDisplay[UNI (uap OR ulm OR uhm) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%
// andys, coa
ItemDisplay[UNI (usk OR urn) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%

// hoz, stormshield, homunc, darkforce
ItemDisplay[UNI (pa9 OR uit OR nea OR nef) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GOLD%

// SET
// aldurs boots, ik chest, trang gloves, trang helm, trang belt, trang shield
ItemDisplay[SET (xtb OR uar OR xmg OR uh9 OR utc OR ne9 ) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GREEN%
// tals chest, tals helm, tals boots, nats chest
ItemDisplay[SET (uth OR xsk OR zmb OR ucl) !ID]: %NL% + %NAME% + %NL%%NOTIFY-GREEN%

// RUNEWORD BASES
// 3os+ superior 15ed mage, dusk, archon
ItemDisplay[SUP (xtp OR uui OR utp) SOCK>2 ED=15 !ETH !RW]: %NL%  %DARK_GREEN%%NAME% +15%ed %RED%%SOCKETS%os  %NL%%NOTIFY-GRAY%
// 3os+ 15ed eth elite armors (treachery, stone, duress)
ItemDisplay[SUP ELT EQ2 SOCK>2 ED=15 ETH !RW]: %NL%  %DARK_GREEN%%NAME% +15%ed %RED%%SOCKETS%os [ETH]  %NL%%NOTIFY-GRAY%
// 4os 15ed eth sacred targe, vortex shield (exile)
ItemDisplay[SUP (pab OR paf) SOCK=4 ED=15 ETH !RW]: %NL%  %DARK_GREEN%%NAME% +15%ed %RED%%SOCKETS%os [ETH]  %NL%%NOTIFY-GRAY%

// RARES >ilvl79
// pelts, gloves, boots, circs
ItemDisplay[RARE (EQ4 OR EQ5 OR EQ7 OR CL1) ILVL>79 !ID]: %NAME% [i%ILVL%]

// hide all else
ItemDisplay[ARMOR (MAG OR RARE OR UNI OR SET) !ID]:
ItemDisplay[ARMOR NMAG !RW]: